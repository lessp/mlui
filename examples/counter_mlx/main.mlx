open Mlui

type button = Increment | Decrement | Reset

module Msg = struct
  type t =
    | Increment
    | Decrement
    | Reset
    | SetColor of Color.t
    | SetHover of button option
end

module Model = struct
  type t = { counter : int; bg_color : Color.t; hovered : button option }

  let init () = { counter = 0; bg_color = Color.gray; hovered = None }
end

let lighten_color (color : Color.t) delta : Color.t =
  let open Color in
  let clamp v = max 0 (min 255 v) in
  let { r; g; b; a } = color in
  { r = clamp (r + delta); g = clamp (g + delta); b = clamp (b + delta); a }

let update msg (model : Model.t) =
  match msg with
  | Msg.Increment ->
      ({ model with Model.counter = model.counter + 1 }, Cmd.none)
  | Msg.Decrement ->
      ({ model with Model.counter = model.counter - 1 }, Cmd.none)
  | Msg.Reset ->
      ({ model with Model.counter = 0 }, Cmd.none)
  | Msg.SetColor color ->
      ({ model with Model.bg_color = color }, Cmd.none)
  | Msg.SetHover hovered ->
      ({ model with Model.hovered }, Cmd.none)

module Styles = struct

  let container background =
    Style.default |> Style.with_flex_grow 1.0
    |> Style.with_flex_direction Column
    |> Style.with_justify_content Center
    |> Style.with_align_items Center
    |> Style.with_background background
    |> Style.with_padding 20

  let counter_text =
    Style.default
    |> Style.with_text_color Color.black
    |> Style.with_font_size 32.0 |> Style.with_padding 20

  let button ~hovered base_color =
    Style.default |> Style.with_padding 15
    |> Style.with_background
         (if hovered then
            lighten_color base_color 40
          else
            base_color)
    |> Style.with_justify_content Center
    |> Style.with_align_items Center

  let button_row =
    Style.default |> Style.with_flex_direction Row |> Style.with_padding 10

  let spacer = Style.default |> Style.with_size ~width:10 ~height:1

  let palette_row =
    Style.default |> Style.with_flex_direction Row |> Style.with_padding 20

  let palette_option color =
    Style.default
    |> Style.with_size ~width:40 ~height:40
    |> Style.with_background color

  let button_text_style =
    Style.default
    |> Style.with_text_color (Color.make ~r:255 ~g:255 ~b:255 ())
    |> Style.with_font_size 24.0

  let reset_text_style =
    Style.default
    |> Style.with_text_color (Color.make ~r:255 ~g:255 ~b:255 ())
    |> Style.with_font_size 18.0
end

(* See components_example.mlx for examples of custom TEA-style components *)

let view (model : Model.t) : Msg.t Mlui.node =
  let open Mlui.Mlx in
  let hovered button =
    match model.hovered with Some btn when btn = button -> true | _ -> false
  in

  <view style=(Styles.container model.bg_color)>
    <text style=Styles.counter_text>
      (string (Printf.sprintf "Count: %d" model.counter))
    </text>

    <view style=Styles.button_row>
      <view
        style=(Styles.button ~hovered:(hovered Decrement) (Color.make ~r:100 ~g:100 ~b:255 ()))
        on_click=(fun () -> Some Msg.Decrement)
        on_mouse_enter=(fun _ -> Some (Msg.SetHover (Some Decrement)))
        on_mouse_leave=(fun _ -> Some (Msg.SetHover None))>
        <text style=Styles.button_text_style>(string " - ")</text>
      </view>

      <view style=Styles.spacer></view>

      <view
        style=(Styles.button ~hovered:(hovered Increment) (Color.make ~r:100 ~g:100 ~b:255 ()))
        on_click=(fun () -> Some Msg.Increment)
        on_mouse_enter=(fun _ -> Some (Msg.SetHover (Some Increment)))
        on_mouse_leave=(fun _ -> Some (Msg.SetHover None))>
        <text style=Styles.button_text_style>(string " + ")</text>
      </view>

      <view style=Styles.spacer></view>

      <view
        style=(Styles.button ~hovered:(hovered Reset) (Color.make ~r:200 ~g:100 ~b:100 ()))
        on_click=(fun () -> Some Msg.Reset)
        on_mouse_enter=(fun _ -> Some (Msg.SetHover (Some Reset)))
        on_mouse_leave=(fun _ -> Some (Msg.SetHover None))>
        <text style=Styles.reset_text_style>(string "Reset")</text>
      </view>
    </view>

    <view style=Styles.palette_row>
      ([Color.green; Color.yellow; Color.magenta; Color.blue]
      |> List.map ( fun color ->
            <view
              style=(Styles.palette_option color)
              on_click=(fun () -> Some (Msg.SetColor color))>
            </view>
      ) |> list )
    </view>
  </view>

let run () =
  let subscriptions _model = Sub.on_quit Msg.Reset in

  let window = Window.make ~width:800 ~height:600 ~title:"Counter" () in
  Mlui.run ~window ~subscriptions ~init:(Model.init ()) ~update ~view ()

let () =
  match run () with
  | Ok () ->
      ()
  | Error (`Msg msg) ->
      Printf.eprintf "Error: %s\n" msg;
      exit 1
